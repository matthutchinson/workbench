#!/usr/bin/env zsh

################################################################################
# Powerline symbols
#
# Use the nerd-font patcher from https://github.com/ryanoasis/nerd-fonts
# to patch built-in SF Mono.
#
#  ./font-patcher -l --octicons --powerline --fontawesome \
#    /Applications/Xcode.app/Contents/SharedFrameworks\
#    /DVTKit.framework/Versions/A/Resources/Fonts/SF-Mono-Regular.otf
#
# (`--fontawesome` required for the colorls gem)
#
################################################################################

local powerline_hard_left_divider='%{%G%}'
local powerline_soft_left_divider='%{%G%}'
local powerline_hard_right_divider='%{%G%}'
local powerline_soft_right_divider='%{%G%}'
local powerline_branch='%{%G%}'
local powerline_line_number='%{%G%}'
local powerline_lock='%{%G%}'
local powerline_vi_edit='%{%G%}'

################################################################################
# Detect editing-mode
################################################################################

function zle-keymap-select() {
  zle reset-prompt
  zle -R
}

zle -N zle-keymap-select

function vi_mode_prompt_info {
  local mode_indicator="%{%K{235}%F{239}%}$powerline_hard_right_divider"
  mode_indicator+="%{%K{239}%F{016}%} $powerline_vi_edit "
  mode_indicator+="%{%K{239}%F{235}%}$powerline_hard_right_divider%{%f%k%}"
  echo "${${KEYMAP/vicmd/$mode_indicator}/(main|viins)/}"
}

################################################################################
# Left
################################################################################

function seasonal_chevrons {
  local date=$(date)
  local chevron='❭'

  case $date in
    # spring
    *Mar*|*Apr*|*May*)
      chevrons="%F{magenta}$chevron%F{cyan}$chevron%F{green}$chevron%f"
      ;;
    # summer
    *Jun*|*Jul*|*Aug*)
      chevrons="%F{cyan}$chevron%F{green}$chevron%F{yellow}$chevron%f"
      ;;
    # fall
    *Sep*|*Oct*|*Nov*)
      chevrons="%F{yellow}$chevron%F{009}$chevron%F{138}$chevron%f"
      ;;
    # winter
    *Dec*|*Jan*|*Feb*)
      chevrons="%F{006}$chevron%F{004}$chevron%F{067}$chevron%f"
      ;;
    *)
      ;;
  esac

  echo -en $chevrons
}

PROMPT="$(seasonal_chevrons) %{%b%f%k%}"

################################################################################
# Right
################################################################################

RPROMPT="%{%K{016}%F{233}%}$powerline_hard_right_divider%{%f%k%}"
RPROMPT+="%{%K{233}%F{234}%}$powerline_hard_right_divider%{%f%k%}"
RPROMPT+="%{%K{234}%F{235}%}$powerline_hard_right_divider%{%f%k%}"

# show editing mode in prompt
RPROMPT+='$(vi_mode_prompt_info)'

# path %~ (truncate start if > 50 chars)
RPROMPT+='%{%K{235}%F{243}%} %50[<…]%~%<< %{%f%k%}'

# show vcs info (see ~/.zsh/vcs_info.zsh)
if hash git >/dev/null 2>&1; then
  RPROMPT+='${vcs_info_msg_0_}'
fi
